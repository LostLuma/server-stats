plugins {
	id 'maven-publish'
	alias libs.plugins.shadow
	alias libs.plugins.quilt.loom
	alias libs.plugins.ploceus
}

apply from: "${rootProject.projectDir}/gradle/common.gradle"

group = project.maven_group
version = generateVersionWithMetadata()

base {
	archivesName = project.archives_base_name
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.layered {
		mappings "net.ornithemc:feather:${project.feather_version}:v2"
		addLayer ploceus.nestedMappings() // Required for nests
	}

	nests "net.ornithemc:nests:${project.nests_version}"
	modImplementation libs.quilt.loader

	shadow libs.gson
	implementation libs.gson

	include project(":versions:1.0.1")
	include project(":versions:1.1.0")
	include project(":versions:1.2.5")
	include project(":versions:1.3.2")
	include project(":versions:1.4.7")
	include project(":versions:1.5.2")
	include project(":versions:1.6.4")
}

shadowJar {
	configurations = [project.configurations.shadow]
	relocate "com.google", "net.lostluma.server_stats.external"
}

remapJar {
	from('LICENSE') // Include license

	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
