plugins {
	id 'maven-publish'
	alias libs.plugins.shadow
	alias libs.plugins.quilt.loom
	alias libs.plugins.ploceus
}

apply from: "${rootProject.projectDir}/gradle/common.gradle"

group = project.maven_group
version = generateVersionWithMetadata()

base {
	archivesName = project.archives_base_name
}

loom {
	serverOnlyMinecraftJar()
}

ploceus {
	serverOnlyMappings()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.layered {
		mappings "net.ornithemc:feather:${project.feather_version}:v2"
		addLayer ploceus.nestedMappings() // Required for nests
	}

	nests "net.ornithemc:nests:${project.nests_version}"
	modImplementation libs.quilt.loader

	shadow libs.gson
	implementation libs.gson

	// 1.0.0 -> Initial release
	include project(":versions:1.0.0-to-1.1.0-alpha.11w48a-client")
	include project(":versions:1.0.1-to-1.1.0-alpha.11w48a-server")
	// 11w49a -> CustomPayloadPacket added
	include project(":versions:1.1.0-alpha.11w49a-to-1.1.0-alpha.11w50a-client")
	include project(":versions:1.1.0-alpha.11w49a-to-1.1.0-alpha.11w50a-server")
	// 12w01a -> Entities.register and MinecraftServer.loadWorld signature changed
	include project(":versions:1.1.0-alpha.12w01a-to-1.2.5-client")
	include project(":versions:1.1.0-alpha.12w01a-to-1.2.5-server")
	// 1.3 snapshots are skipped for now because it's kinda broken (?)
	// And I don't think these are as important as other in-between versions
	include project(":versions:1.3.2-to-1.4.0-alpha.12w38b")
	// 12w39a -> ...
	include project(":versions:1.4.0-alpha.12w39a-to-1.4.7")
	// 13w01a -> added comparator
	include project(":versions:1.5.0-alpha.13w01a-to-1.5.0-alpha.13w01b")
	//13w02a -> ...
	include project(":versions:1.5.0-alpha.13w02a-to-1.5.2")
	// 13w16a -> ...
	include project(":versions:1.6.0-alpha.13w16a-to-1.6.4")
	// 13w36a -> server-side statistics
	include project(":versions:1.7.0-alpha.13w36a-to-1.8.0-alpha.14w05b")
	// 14w06a -> new statistics format, no upgrade path :(
	include project(":versions:1.8.0-alpha.14w06a-to-1.12.2")
}

shadowJar {
	configurations = [project.configurations.shadow]
	relocate "com.google", "net.lostluma.server_stats.external"
}

remapJar {
	from('LICENSE') // Include license

	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
}
